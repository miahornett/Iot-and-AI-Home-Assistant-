#include <WiFi.h>
#include <PubSubClient.h>
#include <Stepper.h> // Stepper Motor Library 

// WiFi Configuaration 
const char* ssid = "IoT-Lab";            // Your WiFi Network Name
const char* password = "ELEC423_2526!";      // Your WiFi Password
const char* mqtt_server = "192.168.1.100";  // IP Address 

const int STEPS_PER_REV = 2048; 
Stepper myStepper(STEPS_PER_REV, 18, 21, 19, 22);

WiFiClient espClient;
PubSubClient client(espClient);

// Function 

void setup_wifi() {
  delay(10);
  Serial.println("Connecting to WiFi...");
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) { delay(500); Serial.print("."); }
  Serial.println("\nWiFi connected");
}

void disableMotor() {
  digitalWrite(18, LOW); digitalWrite(19, LOW);
  digitalWrite(21, LOW); digitalWrite(22, LOW);
  Serial.println("Motor Relaxed (Power Saved)");
}

void callback(char* topic, byte* payload, unsigned int length) {
  String message;
  for (int i = 0; i < length; i++) { message += (char)payload[i]; }
  
  Serial.println("Message arrived [" + String(topic) + "]: " + message);

  if (message == "OPEN") {
    Serial.println("Action: Opening Blinds...");
    myStepper.step(STEPS_PER_REV * 2); 
    disableMotor();
    client.publish("home/livingroom/blinds/status", "OPENED");
  } 
  else if (message == "CLOSE") {
    Serial.println("Action: Closing Blinds...");
    myStepper.step(-STEPS_PER_REV * 2); 
    disableMotor();
    client.publish("home/livingroom/blinds/status", "CLOSED");
  }
}

void reconnect() {
  while (!client.connected()) {
    Serial.print("Attempting MQTT connection...");
    if (client.connect("ESP32_Blind_Actuator")) {
      Serial.println("connected");
      client.subscribe("home/livingroom/blinds"); // Listen for commands
    } else {
      delay(5000);
    }
  }
}

void setup() {
  Serial.begin(115200);
  myStepper.setSpeed(12);
  
  pinMode(18, OUTPUT); pinMode(19, OUTPUT);
  pinMode(21, OUTPUT); pinMode(22, OUTPUT);

  setup_wifi();
  client.setServer(mqtt_server, 1883);
  client.setCallback(callback);
}

void loop() {
  if (!client.connected()) reconnect();
  client.loop();
}
