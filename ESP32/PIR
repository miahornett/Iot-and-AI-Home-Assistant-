#include <WiFi.h>
#include <PubSubClient.h>

// WiFi & MQTT configuration
const char* ssid = "YOUR_WIFI_SSID";
const char* password = "YOUR_WIFI_PASSWORD";
const char* mqtt_server = "IP_ADDRESS_OF_YOUR_RASPBERRY_PI";
const int mqtt_port = 1883;

// MQTT topic
const char* motionTopic = "smart-home/living-room/motion";

// Sensor pin
const int pirPin = 4; // The GPIO pin the PIR sensor's OUT is connected to

// Global state valiable 
int lastMotionState = LOW; // Used to track the sensor's previous state

WiFiClient espClient;
PubSubClient client(espClient);

void setup_wifi() {
  delay(10);
  Serial.println();
  Serial.print("Connecting to ");
  Serial.println(ssid);
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("\nWiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
}

void reconnect() {
  while (!client.connected()) {
    Serial.print("Attempting MQTT connection...");
    String clientId = "ESP32_LivingRoom_PIR"; // Unique Client ID
    if (client.connect(clientId.c_str())) {
      Serial.println("connected");
    } else {
      Serial.print("failed, rc=");
      Serial.print(client.state());
      Serial.println(" try again in 5 seconds");
      delay(5000);
    }
  }
}

void setup() {
  Serial.begin(115200);
  
  // Initalise PIR sensor pin
  pinMode(pirPin, INPUT);
  
  setup_wifi();
  client.setServer(mqtt_server, mqtt_port);
}

void loop() {
  if (!client.connected()) {
    reconnect();
  }
  client.loop();

  // Read PIR sensor 
  int currentMotionState = digitalRead(pirPin);

  // Check is state has changed 
  if (currentMotionState != lastMotionState) {
    
    // State has changed, publish the new state
    if (currentMotionState == HIGH) {
      Serial.println("Motion DETECTED! Publishing to MQTT...");
      client.publish(motionTopic, "DETECTED");
    } else {
      Serial.println("Motion CLEAR. Publishing to MQTT...");
      client.publish(motionTopic, "CLEAR");
    }
    
    // Update the last state
    lastMotionState = currentMotionState;
  }

  // A small delay to prevent the loop from running too fast
  // The PIR sensor itself has a "cooldown" period, but this is good practice.
  delay(100); 
}
