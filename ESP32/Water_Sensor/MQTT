#include <WiFi.h>
#include <PubSubClient.h>

//Wifi Configuration 
const char* ssid = "IoT-Lab";            // Your WiFi Network Name
const char* password = "ELEC423_2526!";      // Your WiFi Password
const char* mqtt_server = "192.168.1.100";  // WiFi IP address 

const int waterSensorPin = 32;

// Threshold 
const int threshold_WaterPresent = 705; // Below this = Water Present
const int threshold_Flood        = 550; // Below this = Large Amount/Flood
const int hysteresis = 20;             // Buffer to prevent state jumping

// State Tracking 
enum WaterState { DRY, WATER_PRESENT, FLOOD_ALERT };
WaterState currentState = DRY;
WaterState lastState    = DRY;

WiFiClient espClient;
PubSubClient client(espClient);

void setup() {
  Serial.begin(115200);
  analogSetAttenuation(ADC_11db);
  setup_wifi();
  client.setServer(mqtt_server, 1883);
}

void loop() {
  if (!client.connected()) reconnect();
  client.loop();

  int val = analogRead(waterSensorPin);

  // Multi-Level State Logic
  WaterState newState = currentState;

  if (val < threshold_Flood) {
    newState = FLOOD_ALERT;
  } 
  else if (val < threshold_WaterPresent && val >= (threshold_Flood + hysteresis)) {
    newState = WATER_PRESENT;
  } 
  else if (val >= (threshold_WaterPresent + hysteresis)) {
    newState = DRY;
  }

  // Event Driven: Only publish on state change
  if (newState != lastState) {
    String message;
    if (newState == DRY)           message = "Dry";
    if (newState == WATER_PRESENT) message = "Water Detected";
    if (newState == FLOOD_ALERT)    message = "FLOOD ALERT";

    Serial.print("Transition to: ");
    Serial.println(message);
    
    client.publish("home/bathroom/water", message.c_str());
    lastState = newState;
    currentState = newState;
  }

  delay(500); 
}

void setup_wifi() {
  delay(10);
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) { delay(500); }
}

void reconnect() {
  while (!client.connected()) {
    if (client.connect("ESP32_Water_Node")) {
      // Re-publish state on connect
    } else { delay(5000); }
  }
}
